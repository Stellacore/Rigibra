#
# MIT License
#
# Copyright (c) 2024 Stellacore Corporation
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in
# all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.
#

##
## -- CMake configuration for downstream use
##


##
## -- create Config.cmake and ConfigVersion.cmake files (for export)
##

# use CMake tools to create package
include(CMakePackageConfigHelpers)

message("### CMAKE_SOURCE_DIR: " ${CMAKE_SOURCE_DIR})

configure_package_config_file(
	${CMAKE_SOURCE_DIR}/cmake/xConfig.cmake.in
	${CMAKE_CURRENT_BINARY_DIR}/${aProjName}Config.cmake
	INSTALL_DESTINATION
		${CMAKE_INSTALL_LIBDIR}/cmake/${aProjName}
	)

write_basic_package_version_file(
	${CMAKE_CURRENT_BINARY_DIR}/${aProjName}ConfigVersion.cmake
	VERSION ${PROJECT_VERSION}
	COMPATIBILITY SameMajorVersion
)

# install the generated configuration files
install(
	FILES
		${CMAKE_CURRENT_BINARY_DIR}/${aProjName}Config.cmake
		${CMAKE_CURRENT_BINARY_DIR}/${aProjName}ConfigVersion.cmake
	DESTINATION
		${CMAKE_INSTALL_LIBDIR}/cmake/${aProjName}
)

##
## -- "publish" export targets for downstream use
##

# create export set for all targets (e.g. public headers and library)
install(
	EXPORT ${EXPORT_TARGETS_NAME}  # e.g. ${aProj}Targets
	FILE ${EXPORT_FILENAME}        # e.g. ${aProj}Targets.cmake
	NAMESPACE ${aProjName}::       # e.g. ${aProj}::${aProj}
	DESTINATION
		${CMAKE_INSTALL_LIBDIR}/cmake/${aProjName}
	)

